#!/bin/zsh --no-rcs
# cbarrick's awesome prompt theme

function prompt_cbarrick_help {
	cat<<'EOF'
Usage: prompt cbarrick [COLOR1 [COLOR2]]
EOF
}

function prompt_cbarrick_setup {
	setopt prompt_subst

	autoload -Uz add-zsh-hook
	autoload -Uz vcs_info

	# Update the version control data before every command
	add-zsh-hook precmd prompt_cbarrick_precmd

	# The colors
	local main_color=${1:-'green'}
	local alt_color=${2:-'red'}

	# The static parts of the prompt; includes the exit status of the last command if not 0
	local main="%(?.%#.%F{${alt_color}}%#(%?%)%f) "

	# These variables are composed into the $vcs_info_msg_0_ environment variable
	local project="%F{${main_color}}%r%f"  # The name of the root directory of the project
	local branch="%F{${main_color}}%b%f"   # The name of the current branch
	local action="%F{${alt_color}}(%a)%f"  # The current action (merge, rebase, etc)
	local vcs_path="%S"       # The path relative to the root of the project
	local non_vcs_path="%1~"  # The name of the current directory

	# Configure vcs_info
	# These options set the $vcs_info_msg_0_ variable dynamically depending on the context:
	# - 'formats' is is the value when in a version controled directory
	# - 'actionformats' is the value during a merge, rebase, etc
	# - 'nvcsformats' is the value when outside of version control
	zstyle ':vcs_info:*:cbarrick:*' formats "${project}:${branch} ${vcs_path}"
	zstyle ':vcs_info:*:cbarrick:*' actionformats "${project}:${branch}${action} ${vcs_path}"
	zstyle ':vcs_info:*:cbarrick:*' nvcsformats "${non_vcs_path}"

	# The prompt is the dynamic vcs info followed by the static parts of the prompt
	PROMPT='${(e)vcs_info_msg_0_} '
	PROMPT+="${main}"
	export PROMPT

	# SSH Hostname on the right.
	export RPROMPT='${SSH_CONNECTION:+ %m}'
}

function prompt_cbarrick_precmd {
	vcs_info cbarrick
}

prompt_cbarrick_setup "$@"
