#!/bin/zsh --no-rcs
# cbarrick's awesome prompt theme

function prompt_cbarrick_help {
	cat<<'EOF'
Usage: prompt cbarrick [<main_color=green> [<info_color=blue> [<error_color=red>]]]
EOF
}

function prompt_cbarrick_precmd {
	# We reset the prompt every time since the vcs_info messages may change.
	# Alternativly, we could `setopt prompt_subst` and just set the prompt once,
	# but that is less transparent, e.g. `echo $PROMPT` won't tell us much.
	vcs_info prompt_cbarrick
	PROMPT="${vcs_info_msg_0_}"
	RPROMPT="${vcs_info_msg_1_}"
}

function prompt_cbarrick_setup {
	autoload -Uz add-zsh-hook
	autoload -Uz vcs_info

	# The prompt is reset every precmd.
	# The prompt library automatically cleans up when the prompt is deactivated.
	add-zsh-hook precmd prompt_cbarrick_precmd

	# Prompt options to enable while this theme is active,
	# e.g. `prompt_opts=(percent)` is like `setopt prompt_percent`, but the
	# option is reverted to its original value when the theme is disabled.
	# http://zsh.sourceforge.net/Doc/Release/Options.html#Prompting
	prompt_opts=(cr percent)

	# Set colors from the command line arguments, with sane defaults.
	local main_color=${1:-'green'}
	local info_color=${2:-'blue'}
	local err_color=${3:-'red'}

	# Pieces of the prompt.
	# These may include both prompt expansions and vcs_info expansions.
	# When ambiguous (or buggy), write `%%` to specify prompt expansion.
	# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
	# http://zsh.sourceforge.net/Doc/Release/User-Contributions.html#Version-Control-Information
	local repo="%F{${main_color}}%r%f"
	local branch="%F{${main_color}}%b%f"
	local action="%F{${info_color}}(%a)%f"
	local vcs_path="%S"       # The path relative to the root of the repo.
	local non_vcs_path="%1~"  # The name of the current directory.
	local main="%%(1j.%F{${info_color}}[%j]%f .)%%(?..%F{${err_color}})%#%f "
	local ssh_conn=" ${SSH_CONNECTION:+%%m}"

	# The prompt is determined by `vcs_info prompt_cbarrick`.
	# The messages become PROMPT and RPROMPT respectivly.

	# The prompt when inside a version controled directory.
	zstyle ':vcs_info:*:prompt_cbarrick:*' formats \
		"${repo}:${branch} ${vcs_path} ${main}" \
		"${ssh_conn}"

	# The prompt during a merge, rebase, etc.
	zstyle ':vcs_info:*:prompt_cbarrick:*' actionformats \
		"${repo}:${branch} ${action} ${vcs_path} ${main}" \
		"${ssh_conn}"

	# The prompt outside of version control.
	# In this case, vcs_info does NOT expand, so we manually replace s/%%/%.
	zstyle ':vcs_info:*:prompt_cbarrick:*' nvcsformats \
		"${non_vcs_path} ${main:gs/%%/%}" \
		"${ssh_conn:gs/%%/%}"
}

prompt_cbarrick_setup "$@"
