#!/bin/zsh --no-rcs
# cbarrick's awesome prompt theme

function prompt_cbarrick_help {
	cat<<'EOF'
Usage: prompt cbarrick [COLOR1 [COLOR2]]
EOF
}

function prompt_cbarrick_setup {
	local main_color	# Main info color; used for branch name etc
	local alt_color	 # Secondary info color; used for errors, actions
	local main		  # The universal part
	local vcs_part	  # Part that shows version control info
	local path_part	 # Part of the prompt that shows
	local action_part   # Part that shows vcs actions like in progress merges
	local vcs_prompt	# Prompt when in a version controled directory
	local no_vcs_prompt # Prompt when in a non-vcs directory

	main_color=${1:-'green'}
	alt_color=${2:-'red'}

	setopt prompt_subst

	autoload -Uz add-zsh-hook
	autoload -Uz vcs_info

	# Update the prompt before every command.
	add-zsh-hook precmd prompt_cbarrick_precmd

	# The main prompt
	main="%(?..%F{${alt_color}})%#%f"

	# In version control: `<project>:<branch> <path> <prompt>`.
	vcs_part="%F{${main_color}}%r%f:%F{${main_color}}%b%f"
	action_part="%F{${alt_color}}\${vcs_info_msg_1_}%f"
	vcs_prompt="${vcs_part}${action_part} %S %${main} "

	# Not in version control: `<dirname> <prompt>`.
	no_vcs_prompt="%1~ ${main} "

	# Settings for the vcs_info plugin, where the prompt is _actually_ set.
	zstyle ':vcs_info:*' formats "${vcs_prompt}"
	zstyle ':vcs_info:*' actionformats "${vcs_prompt}" "(%a)"
	zstyle ':vcs_info:*' nvcsformats "${no_vcs_prompt}"

	# The main prompt is the main message output by the vcs_info plugin.
	export PROMPT='${(e)vcs_info_msg_0_}'

	# SSH Hostname on the right.
	export RPROMPT='${SSH_CONNECTION:+ %m}'
}

function prompt_cbarrick_precmd {
	# Update the version control info.
	vcs_info
}

prompt_cbarrick_setup "$@"
