## vim:ft=zsh
## Written by Frank Terbeck <ft@bewatermyfriend.org>
## Distributed under the same BSD-ish license as zsh itself.

setopt localoptions noksharrays NO_shwordsplit
local c v rr

if [[ $1 == '-preinit-' ]] ; then
    c='default'
    v='-preinit-'
    rr='-all-'
fi
zstyle -a ":vcs_info:${v:-$vcs}:${c:-$usercontext}:${rrn:-$rr}" nvcsformats msgs
(( ${#msgs} > maxexports )) && msgs[${maxexports},-1]=()

# The following loop was added by Chris Barrick
#
# The problem: I use vcs_info in my prompt, which in turn uses zformat to expand %-formats.
# zformat will always evaluate [ternary expressions][13.3] even if the corresponding character
# isn't to be expanded. Thus, in my prompt, I escape my ternary expressions with an extra `%`
# to pass them through to the prompt expansion. However, zformat *is not called* by vcs_info
# during nvcsformats. So I have to maintain another prompt without escapes for that case...
#
# The solution: Make vcs_info expand %-formats in nvcsformats. Since there's no vcs from
# which to get info, we expand characters to themselves
#
# [13.3]: http://tinyurl.com/zsh-13-3

for i in {1..$((${#msgs} + 1))} ; do
    zformat -f msg ${msgs[$i]} \
                    a:a \
                    b:b \
                    c:c \
                    i:i \
                    m:m \
                    r:r \
                    s:s \
                    u:u \
                    Q:Q \
                    R:R \
                    S:S
    msgs[$i]=${msg}
done

return 0
